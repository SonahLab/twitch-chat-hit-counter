plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'idea'
    id 'nebula.dependency-lock' version '12.7.1'
}

group = 'com.sonahlab'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

// Enable dependency locking
dependencyLocking {
    lockAllConfigurations()
}

ext {
    testContainerVersion = '1.20.2'
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Spring Boot frameworks
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'

    // Module 1: Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
    runtimeOnly 'org.jetbrains.kotlin:kotlin-reflect:2.1.0'

    // Module 2: Kafka
    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // Module 3: MySql
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'mysql:mysql-connector-java:8.0.33'

    // Module 4: Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Module 5: Twitch API
    implementation 'com.github.twitch4j:twitch4j:1.25.0'

    // Testcontainers
    implementation "commons-codec:commons-codec:1.17.1" // Needed to work with mysql testcontainer
    testImplementation "org.testcontainers:junit-jupiter:$testContainerVersion"
    testImplementation "org.testcontainers:kafka:$testContainerVersion"
    testImplementation "org.testcontainers:mysql:$testContainerVersion"
    testImplementation "com.redis:testcontainers-redis:2.2.4"
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    useJUnitPlatform {
        // Allows for individual module unit testing using @Tag("moduleX")
        // Example usage: ./gradlew test --tests "*" -Ptags=Module1
        if (project.hasProperty("tags")) {
            includeTags project.tags
        }
    }
}

configurations {
    all {
        // Exclude spring-boot-starter-logging as it is incompatible with spring-boot-starter-log4j2
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}
